apply plugin: 'com.android.model.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
// Example of file content:
//      storePassword=password
//      keyPassword=password
//      keyAlias=AliasInKeyStore
//      storeFile=MyAndroidKeys.jks
def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

model {
    android {
        compileSdkVersion = 24
        buildToolsVersion = "24.0.1"

        defaultConfig {
            applicationId = "ru.testsimpleapps.lifewallpaperabstract"
            minSdkVersion.apiLevel = 10
            targetSdkVersion.apiLevel = 24
            versionCode = 8
            versionName = "1.0.7"
        }

        ndk {
            platformVersion = 9
            moduleName = 'SimpleWallpaper'
            toolchain = 'clang'
            stl = 'gnustl_static'

            // Flags for clang
            CFlags.addAll(['-DANDROID_NDK', '-DDISABLE_IMPORTGL'])
            CFlags.addAll(['-Wall', '-Werror'])

            // For add external files
            cppFlags.addAll(['-std=c++11', '-I' + file('src/main/jni/')])

            // Library for include
            ldLibs.addAll(['log', 'GLESv2', 'android', 'dl'])
        }

        buildTypes {
            release {
                minifyEnabled = false
                proguardFiles.add(file("proguard-rules.pro"))
                signingConfig = $("android.signingConfigs.signRelease")
            }
        }
    }

    android.signingConfigs {
        create("signRelease") {
            keyAlias = keystoreProperties['keyAlias']
            keyPassword = keystoreProperties['keyPassword']
            storeFile = file(keystoreProperties['storeFile'])
            storePassword = keystoreProperties['storePassword']
        }
    }
}